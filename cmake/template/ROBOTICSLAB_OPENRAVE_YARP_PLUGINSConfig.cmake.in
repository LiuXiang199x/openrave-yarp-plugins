# Authors: Juan G Victores
# CopyPolicy: Released under the terms of the LGPLv2.1 or later, see LGPL.TXT

# Once you find_package(ROBOTICSLAB_OPENRAVE_YARP_PLUGINS), it defines:
#
# * several targets; it's advised to link against individual libraries with the usual
#   target_link_libraries() command
#
# * ROBOTICSLAB_OPENRAVE_YARP_PLUGINS_INCLUDE_DIRS; use include_directories(${ROBOTICSLAB_OPENRAVE_YARP_PLUGINS_INCLUDE_DIRS})
#
# * ROBOTICSLAB_OPENRAVE_YARP_PLUGINS_LIBRARIES; shorthand for linking against all targets at once,
#   use target_link_libraries(_target ${ROBOTICSLAB_OPENRAVE_YARP_PLUGINS_LIBRARIES})
#
# Notes:
#
# * All target names are prepended with the ROBOTICSLAB:: namespace

@PACKAGE_INIT@

set(ROBOTICSLAB_OPENRAVE_YARP_PLUGINS_INCLUDE_DIRS)

foreach(_dir @PACKAGE_ROBOTICSLAB_OPENRAVE_YARP_PLUGINS_INCLUDE_DIR@)
    set_and_check(_temp_var ${_dir})
    list(APPEND ROBOTICSLAB_OPENRAVE_YARP_PLUGINS_INCLUDE_DIRS ${_temp_var})
endforeach()

if(NOT "@_exported_targets@" STREQUAL "")
    include(${CMAKE_CURRENT_LIST_DIR}/ROBOTICSLAB_OPENRAVE_YARP_PLUGINSTargets.cmake)

    set(ROBOTICSLAB_OPENRAVE_YARP_PLUGINS_LIBRARIES)

    foreach(_target @_exported_targets@)
        list(APPEND ROBOTICSLAB_OPENRAVE_YARP_PLUGINS_LIBRARIES ROBOTICSLAB::${_target})
    endforeach()
endif()
